/*
 * Copyright 2020 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'java-library'

apply from: "${project.scripts_url}/${project.scripts_branch}/build-quality.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/release-commons.gradle"
apply from: "${project.scripts_url}/${project.scripts_branch}/signing.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    api 'com.epam.reportportal:client-java:5.0.21'
    api 'com.epam.reportportal:commons-model:5.0.0'
    api 'com.nordstrom.tools:junit-foundation:15.1.3-SNAPSHOT'
    api 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation 'com.github.reportportal:agent-java-test-utils:ddcf50ee20'
    testImplementation 'org.aspectj:aspectjweaver:1.9.2'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    testImplementation "org.mockito:mockito-core:${project.mockito_version}"
    testImplementation "org.mockito:mockito-junit-jupiter:${project.mockito_version}"
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'com.epam.reportportal:logger-java-logback:5.0.3'
    testImplementation ("org.junit.platform:junit-platform-runner:${project.junit5_runner_version}") {
        exclude module: 'junit'
    }
    testImplementation "org.junit.jupiter:junit-jupiter-api:${project.junit5_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${project.junit5_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${project.junit5_version}"
    testImplementation 'org.apache.commons:commons-io:1.3.2'
    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
    testImplementation "org.powermock:powermock-module-junit4:${project.powermock_version}"
    testImplementation "org.powermock:powermock-api-mockito2:${project.powermock_version}"
}

test {
    debug true
    outputs.upToDateWhen { return false }
    useJUnitPlatform()
    maxParallelForks(5)
    forkEvery(1)
    doFirst {
        def junitFoundation = configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts.find { it.name == 'junit-foundation' }
        def weaver = configurations.testRuntimeClasspath.find { it.name.contains("aspectjweaver") }
        jvmArgs += "-javaagent:${junitFoundation.file}"
        jvmArgs += "-javaagent:$weaver"
    }

    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

wrapper {
    gradleVersion = '5.4.1'
}

processResources {
    filesMatching('agent.properties') {
        expand(project.properties)
    }
}
